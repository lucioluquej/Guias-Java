Algoritmo sin_titulo
	
//	El algoritmo principal sólo debe llamar al subPrograma menu()
//	Cada subPrograma puede descomponerse, si hiciera falta, en otros subProgramas a creatividad
//	del programador
//	El menú debe quedar de la siguiente manera:
//	1. Calcular muro de ladrillo
//	2. Calcular viga de hormigón
//	3. Calcular columnas de hormigón
//	4. Calcular contrapisos
//	5. Calcular techo
//	6. Calcular pisos
//	7. Calcular pintura
//	8. Calcular iluminación
//	9. Salir
	
//	subprogramas calcularSuperficie y calcularVolumen
//	Haremos ambos para usarlos dentro de los otros subprogramas. El usuario no puede acceder a
//	ellos.
//				
//				2
//				
//	subprograma calcularMuro
//	Nos debe pedir primero si el muro será de 20 o 30 cm de espesor. Luego el largo y el alto. A partir
//	de estos datos se debe mostrar al usuario la superficie del muro y la cantidad de materiales que
//	necesitaremos para construirlo.
//	Si el muro es de 30cm necesitaremos por metro cuadrado: 15.2 kg de cemento, 0.115 m3 de arena
//	y 120 ladrillos.
//	Si el muro es de 20cm necesitaremos por metro cuadrado: 10.9 kg de cemento, 0.09 m3 de arena
	//	y 90 ladrillos.
	
//	subprograma calcularViga
//	Nos debe pedir el largo de la viga. Por metro lineal de viga se necesitarán: 9 kg de cemento, 0.02
//	m3 de arena, 0.02 m2 de piedra, 4 m de hierro del 8 y 3 m de hierro del 4.
	//	Debemos mostrar al usuario la cantidad de materiales necesaria.
	
//	subprograma calcularColumna
//	Nos debe pedir el largo de la columna. Por metro lineal de columna se necesitarán: 7.5 kg de
//	cemento, 0.016 m3 de arena, 0.016 m2 de piedra, 6 m de hierro del 10 y 3 m de hierro del 4.
//	Debemos mostrar al usuario la cantidad de materiales necesaria.
	
//	subprograma calcularContrapisos
//	Nos debe pedir espesor, ancho y largo del contrapiso a calcular.
//	Por metro cúbico de contrapiso se necesita: 105 kg de cemento, 0.45 m3 de arena y 0.9 m3 de
//	piedra.
//	Debemos mostrar al usuario la cantidad de materiales necesaria.
	
//	subprograma calcularTecho
//	Nos debe pedir espesor, ancho y largo del techo a calcular.
//	Por metro cuadrado de techo se necesita: 33 kg de cemento, 0.072 m3 de arena, 0.072 m3 de
//	piedra, 7 m de hierro del 8 y 4 m de hierro del 6
//	Debemos mostrar al usuario la cantidad de materiales necesaria.
	
//	subprograma calcularPisos
//	Nos debe pedir ancho y largo del paño de piso a colocar. Teniendo esos datos se debe calcular la
//	superficie y añadirle un 10% extra por recortes
//	Mostrar el resultado en m2
	
//	subprograma calcularPintura
//	Nos debe pedir la superficie del muro y mostrar cuánta pintura necesitamos teniendo en cuenta
//	que rinde 6 m2 por litro de pintura.
	
//	subprograma calcularIluminacion
//	Nos debe pedir la superficie de la habitación. La iluminación la calculamos de la siguiente forma:
//	superficie * 0.20. Eso nos da la cantidad mínima de superficie de iluminación natural (ventanas y
//	puertas de vidrio). Mostrar resultado
	
	Definir menu,espesor,largo,alto,resultado Como Entero
	
	hacer
		Escribir "1. Calcular muro de ladrillo. 2. Calcular viga de hormigón 3. Calcular columnas de hormigón 4. Calcular contrapisos 5. Calcular techo 6. Calcular pisos 7. Calcular pintura 8. Calcular iluminación"
		leer menu
		
		Segun menu
			1: calcularmuroladrillo()
			2: calcularvigahormigon()
			3: calcularcolumnashormigon()
			4: calcularcontrapiso()
			5: calculartecho()
			6: calcularpiso()
			7: calcularpintura()
			8: calculariluminacion()
			De Otro Modo:
				Escribir "Gracias por su visita"
				menu = 9
	FinSegun
Mientras Que menu <9
	
FinAlgoritmo

SubProceso  calcularmuroladrillo()
	Definir espesor, largo, alto, superficie, ladrillos, cemento, arena Como Real
	
	Escribir "Ingrese el espesor"
	leer espesor
	
	Escribir "Ingrese el largo"
	leer largo
	
	Escribir "Ingrese el alto"
	leer alto
	
	superficie = calcularsuperficie(largo,alto)
	
	si espesor = 30
		cemento = 15.2 * superficie
		arena = 0.115 * superficie
		ladrillos = 120 * superficie
	finsi
	si espesor = 20
		cemento = 10.9 * superficie
		arena = 0.09 * superficie
		ladrillos = 90 * superficie
	FinSi
	
	
	Escribir superficie "m2"
	Escribir cemento "kg"
	Escribir arena "m3"
	Escribir ladrillos " unidades"
	
FinSubProceso

SubProceso  calcularvigahormigon()
	Definir largo,cemento,arena,piedra,hierro8,hierro4 Como Real
	
	Escribir "Ingrese el largo de la viga"
	leer largo
	
	cemento = 9 * largo
	arena = 0.02 * largo
	piedra = 0.02 * largo
	hierro8 = 4 * largo
	hierro4 = 3 * largo
	
	Escribir cemento "kg"
	Escribir arena "m3"
	Escribir piedra "m2"
	Escribir hierro8 "m"
	Escribir hierro4 "m"
	
FinFuncion

SubProceso  calcularcolumnashormigon()
	Definir largo,cemento, arena , piedra , hierro10, hierro4 Como Real
	
	Escribir "Ingrese el largo de la columna"
	leer largo
	
	cemento = 7.5 * largo
	arena = 0.016 * largo
	piedra = 0.016 * largo
	hierro10 = 6 * largo
	hierro4 = 3 * largo
	
	Escribir cemento "kg"
	Escribir arena "m3"
	Escribir piedra "m2"
	Escribir hierro10 "m"
	Escribir hierro4 "m"
	
FinFuncion

SubProceso  calcularcontrapiso()
	Definir espesor,alto, largo,cemento,arena,piedra,volumen Como Real
	
	Escribir "Ingrese el espesor"
	leer espesor
	
	Escribir "Ingrese el ancho"
	leer alto
	
	Escribir "Ingrese el largo"
	leer largo
	
	volumen = calcularvolumen(largo,alto,espesor)
	
	cemento = 105 * volumen
	arena = 0.45 * volumen
	piedra = 0.9 * volumen

	
	Escribir cemento "kg"
	Escribir arena "m3"
	Escribir piedra "m2"

	
FinFuncion

SubProceso  calculartecho()
	Definir espesor,alto, largo,cemento,arena,piedra,superficie,hierro8,hierro6 Como Real
	
	Escribir "Ingrese el espesor"
	leer espesor
	
	Escribir "Ingrese el ancho"
	leer alto
	
	Escribir "Ingrese el largo"
	leer largo
	
	superficie = calcularsuperficie(largo,alto)
	
	cemento = 33 * superficie
	arena = 0.072 * superficie
	piedra = 0.072 * superficie
	hierro8 = 7 * superficie
	hierro6 = 4 * superficie
	
	Escribir cemento "kg"
	Escribir arena "m3"
	Escribir piedra "m2"
	Escribir hierro8 "m"
	Escribir hierro6 "m"
	
FinFuncion

SubProceso  calcularpiso()
	Definir ancho,largo,superficie Como Real
	
	Escribir "Ingrese el ancho"
	leer ancho
	
	Escribir "Ingrese el largo"
	leer largo
	
	superficie = calcularsuperficie(ancho,largo)
	
	Escribir superficie * 0.1 + superficie "m2"
	
FinFuncion
	
SubProceso  calcularpintura()
	Definir ancho,largo,superficie Como Real
	
	Escribir "Ingrese el ancho"
	leer ancho
	
	Escribir "Ingrese el largo"
	leer largo
	
	superficie = calcularsuperficie(ancho,largo) / 6
	
	Escribir superficie "litros"
	
FinFuncion

SubProceso  calculariluminacion()
	Definir ancho,largo,superficie Como Real
	
	Escribir "Ingrese el ancho"
	leer ancho
	
	Escribir "Ingrese el largo"
	leer largo
	
	superficie = calcularsuperficie(ancho,largo) * 0.2
	
	Escribir superficie " m2 de iluminacion"
	
FinFuncion

Funcion superficie = calcularsuperficie(largo,alto)
	Definir superficie Como Real
	
	superficie = largo * alto
	
FinFuncion

Funcion volumen = calcularvolumen(largo,ancho,espesor)
	Definir volumen Como Real
	
	volumen = largo * ancho * espesor
FinFuncion
	